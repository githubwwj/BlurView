apply plugin: 'com.android.library'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    compileSdkVersion 36
    namespace "com.appxy.tinyscanner"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
        testInstrumentationRunner "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'junit-jupiter'
                }
            }
        }
    }
}

configurations {
    javadocDeps
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.core:core:1.16.0'
    javadocDeps 'androidx.annotation:annotation:1.9.1'

//    implementation 'com.caverock:androidsvg:1.4' // 最新稳定版
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.4'
}

task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    from androidJavadocs.destinationDir
    archiveClassifier = 'javadoc'
}

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

